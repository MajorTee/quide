
buildscript {
    ext.kotlin_version = '1.0.5-2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
	id 'com.github.ben-manes.versions' version '0.13.0'
	id "com.github.johnrengelman.shadow" version "1.2.3"
//		id "com.github.hierynomus.license" version "0.13.1"
}

allprojects {
	group 'io.gitlab.arturbosch'
	version "$quideVersion"
}

subprojects {

	repositories {
		mavenCentral()
		mavenLocal()
	}

	apply plugin: 'java'
	apply plugin: 'maven'

	if (it.name.startsWith("quide") && it.name != "quide-specification") {
		apply plugin: 'kotlin'
	}

	if (it.name == "quide-platform") {
		apply plugin: 'application'
		apply plugin: 'com.github.johnrengelman.shadow'
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: uri('../repo'))
			}
		}
	}

	configurations {
		kotlin
		spock
	}

	dependencies {
		kotlin "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
		spock "org.spockframework:spock-core:$spockVersion"
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
	}

}
